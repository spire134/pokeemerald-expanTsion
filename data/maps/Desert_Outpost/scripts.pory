mapscripts Desert_Outpost_MapScripts {}

script Rocky_Road_Rest_Lady {
    lock
    msgbox(format("Resting here in the desert outpost is a great way to recover your strength!\p"
    "Would you like to take a break and heal your POKéMON?"), MSGBOX_YESNO)
    if (var(VAR_RESULT) == NO) {
        msgbox(format("Alright, let me know if you need anything."))
        waitmessage
        release
    } else {
    special(HealPlayerParty)
    playse(MUS_HEAL)
    waitse
    msgbox(format("Your POKéMON have been healed."))
    waitmessage
    release
}
}

script Rocky_Road_Trader {
    lock
    faceplayer
    msgbox(format("Trader Joe: Hey there, traveler!\p"
    "Looking to trade some items? I've got a few rare things you might be interested in.\p"
    "Take a look and see if anything catches your eye!"))
    waitmessage
    pokemart(POKEMART_TRADER_JOE)
    msgbox(format("Trader Joe: Thanks for stopping by! Come back anytime if you want to trade more items."))
    waitmessage
    release
}

mart POKEMART_TRADER_JOE {
    ITEM_RARE_BONE
    ITEM_RAGE_CANDY_BAR
    ITEM_BIG_MUSHROOM
    ITEM_PEARL
    ITEM_NUGGET
    ITEM_OVAL_STONE   
}

script Rocky_Road_Extremist {
    lock
    faceplayer
    msgbox(format("The wastes of Corymbra prove a harsh place, but it reveals the true strength of one's character!\p"
    "Only those who can endure its trials are worthy of the title."))
    waitmessage
    release
}

script Rocky_Road_Thirsty_Lileep_Trade {
    lock
    faceplayer
    goto_if_set(FLAG_PACIFIDLOG_NPC_TRADE_COMPLETED, Lileep_Trade_Completed)
    setvar(VAR_0x8008, INGAME_TRADE_LILEEP)
    copyvar(VAR_0x8004, VAR_0x8008)
    specialvar(VAR_RESULT, GetInGameTradeSpeciesInfo)
    copyvar(VAR_0x8009, VAR_RESULT)
    msgbox(format("Ace Chuck: Hey there, ranger! You think you can help me out?\p"
    "I'm always so bone dry thirsty out here, if you could bring me a {STR_VAR_1}, I'd be forever grateful.\p"
    "Why, I'll trade you this here Lileep, after all, it keeps draining all the water near it!"
    ), MSGBOX_YESNO)
    goto_if_eq(VAR_RESULT, NO, Lileep_Trade_DeclineTrade)
    special(ChoosePartyMon)
    waitstate
    copyvar(VAR_0x800A, VAR_0x8004)
    goto_if_eq(VAR_0x8004, PARTY_NOTHING_CHOSEN, Lileep_Trade_DeclineTrade)
    copyvar(VAR_0x8005, VAR_0x800A)
    specialvar(VAR_RESULT, GetTradeSpecies)
    copyvar(VAR_0x800B, VAR_RESULT)
    goto_if_ne(VAR_RESULT, VAR_0x8009, Lileep_Trade_NotRequestedMon)
    copyvar(VAR_0x8004, VAR_0x8008)
    copyvar(VAR_0x8005, VAR_0x800A)
    special(CreateInGameTradePokemon)
    special(DoInGameTradeScene)
    waitstate
    msgbox(format(
        "Ace Chuck: Ah, that's the stuff! Thanks a ton, ranger!\p"
        "Here's your Lileep, take good care of it out here in the desert!"
    ))
    setflag(FLAG_PACIFIDLOG_NPC_TRADE_COMPLETED)  
    release
}

script Lileep_Trade_DeclineTrade {
    msgbox(format(
        "Ace Chuck: Ah, no worries then. Stay hydrated out there!"
    ))
    release
}

script Lileep_Trade_NotRequestedMon {
    bufferspeciesname(STR_VAR_1, VAR_0x8009)
    msgbox(format(
        "Ace Chuck: Hmm, this isn't a {STR_VAR_1}…\p"
        "I really need that to quench my thirst out here."
    ))
    release
}


