mapscripts Tidesmercyinterior_MapScripts {}


script PlayerRoom_Note {
    lock
    if (!flag(FLAG_NOTE_READ)) {
        msgbox(format(
            "A crisp, professional note from the Captain.\p"
            "RANGER RECRUIT,\p"
            "Congratulations on passing your boards. Please acquaint yourself with your fellow Rangers,"
            "then report to my quarters for partner assignment at your earliest convenience.\p"
            "Professional enthusiasm is encouraged.\n"
            "- Captain Mara Kealoha"
        ))
        setflag(FLAG_NOTE_READ)
    } else {
        msgbox(format("
        “Report to my quarters for partner assignment.”
         - Captain Mara Kealoha"))
    }
    setflag(FLAG_SYS_B_DASH)
    setflag(FLAG_RECEIVED_RUNNING_SHOES)
    setflag(FLAG_SYS_POKEDEX_GET)
    setflag(FLAG_SYS_NATIONAL_DEX)
    special(EnableNationalPokedex)
    special(SetUnlockedPokedexFlags)
    setflag(FLAG_RECEIVED_POKEDEX_FROM_BIRCH)
    msgbox(format("You grabbed your gear and shoes. Time to meet the Captain."))
    release
    end
}

script Should_Read_Note {
    if (!flag(FLAG_NOTE_READ)) {
    lock
    msgbox(format(
        "Don't forget to read the note the Captain left on your desk!\n"
        ))
    waitbuttonpress
    applymovement(LOCALID_PLAYER, moves(walk_up))
    waitmovement(0)
    release
    end
    } else {
    end
    }}



# =========================================
# 2) OPTIONAL SQUADMATE CHATS (interior)
# Two share a room; one solo.
# =========================================

# Shared room pair: awkward but sweet dynamic
script RangerA_Script {
    lock
    faceplayer
    msgbox(format(
        "Tasmin: I'm Ranger Tasmin. Logistics and operations coordination.\l"
        "I maintain equipment manifests, communication protocols, and mission schedules.\p"
        "Before you ask--yes, some call me 'quartermaster.' I prefer not to.\l"
        "It makes me sound like a pirate.\p"
        "When you meet the Captain, I recommend selecting Turtwig as your partner.\l"
        "Sturdy. Reliable. Excellent weight distribution for technical climbing.\p"
        "The data supports this choice."
    ))
    release
    end
}

script RangerB_Script {
    lock
    faceplayer
    msgbox(format(
        "Corin: Hey! I'm Ranger Corin. Ropes, rescue, and keeping people from\l"
        "becoming permanent fixtures on cliff faces. Welcome to Steel Team.\p"
        "If you're smart, you'll pick Torchic when you see the Captain.\l"
        "Fast, fierce, and it'll warm you right up when the storms hit.\p"
        "Plus, Tasmin keeps saying I need to stop 'playing favorites' with the--\p"
        "Tasmin: CORIN.\p"
        "Corin: --with the starter selection process. Very professional concern from our\l"
        "quarter--I mean, logistics coordinator."
    ))
    release
    end
}

# SOLO ROOM EON
script RangerSolo_Script {
    lock
    faceplayer
    msgbox(format(
        "Eon: Ranger Eon. Reconnaissance and cartography.\l"
        "I chart what others miss.\p"
        "You passed the boards. Congratulations. But Corymbra isn't a test.\l"
        "It's a graveyard for careless Rangers.\p"
        "The Captain will offer you a partner. If you value stealth and adaptability,\l"
        "choose Froakie. It moves through hostile terrain without announcing itself.\p"
        "Unlike most recruits."
    ))
    release
    end
}

# =========================================
# 3) CAPTAIN'S ROOM: mission brief + starters
# =========================================

script CaptainRoom_Talk {
    lockall
    faceplayer
    if (!flag(FLAG_GOT_STARTER)) {
        msgbox(format(
            "Captain Mara: Welcome aboard, Ranger.\p"
            "I hope you are well prepared for your first mission as a Ranger.\p"
            "There was a blackout on communications from the Ranger teams on the southern shore
            of Corymbra Island. Our mission is to investigate and restore contact.\p"
            "Before we depart, you need to choose your partner Pokémon.\l"
            "You have three choices: Turtwig, Torchic, or Froakie."
        ))
    } else {
        msgbox("You have your partner. Good. Stay flexible.")
    }
    releaseall
    end
}

# Place three Poké Ball objects in this room and hook these scripts
script Starter_Turtwig {
    lockall
    if (flag(FLAG_GOT_STARTER)) { msgbox("The case is empty.") releaseall end }
    showmonpic(SPECIES_TURTWIG, 10, 3)
    msgbox("Take TURTWIG as your partner?", MSGBOX_YESNO)
    if (var(VAR_RESULT) == 0) { 
        hidemonpic    
        releaseall 
        end
    }
    call(Give_Starter0)
    call(PostStarter_StormInterrupt)  # immediate story beat
    releaseall
    end
}

script Starter_Torchic {
    lockall
    if (flag(FLAG_GOT_STARTER)) { msgbox("The case is empty.") releaseall end }
    showmonpic(SPECIES_TORCHIC, 10, 3)
    msgbox("Take TORCHIC as your partner?", MSGBOX_YESNO)
    if (var(VAR_RESULT) == 0) { 
        hidemonpic 
        releaseall 
        end 
    }
    call(Give_Starter1)
    call(PostStarter_StormInterrupt)
    releaseall
    end
}

script Starter_Froakie {
    lockall
    if (flag(FLAG_GOT_STARTER)) { msgbox("The case is empty.") releaseall end }
    showmonpic(SPECIES_FROAKIE, 10, 3)
    msgbox("Take FROAKIE as your partner?", MSGBOX_YESNO)
    if (var(VAR_RESULT) == 0) { 
        hidemonpic 
        releaseall 
        end
    }
    call(Give_Starter2)
    call(PostStarter_StormInterrupt)
    releaseall
    end
}

script(local) Give_Starter0 {
    hidemonpic
    setflag(FLAG_SYS_POKEMON_GET)
    setvar((VAR_STARTER_MON), 0)
    givemon(SPECIES_TURTWIG, 5)
    call(Common_EventScript_NameReceivedPartyMon)
    setflag(FLAG_GOT_STARTER)
    playfanfare(FANFARE_RG_OBTAIN_KEY_ITEM)
    msgbox("TURTWIG joined your squad.")
    return
}
script(local) Give_Starter1 {
    hidemonpic
    setflag(FLAG_SYS_POKEMON_GET)
    setvar((VAR_STARTER_MON), 1)
    givemon(SPECIES_TORCHIC, 5)
    call(Common_EventScript_NameReceivedPartyMon)
    setflag(FLAG_GOT_STARTER)
    playfanfare(FANFARE_RG_OBTAIN_KEY_ITEM)
    msgbox("TORCHIC joined your squad.")
    return
}
script(local) Give_Starter2 {
    hidemonpic
    setflag(FLAG_SYS_POKEMON_GET)
    setvar(VAR_STARTER_MON, 2)
    givemon(SPECIES_FROAKIE, 5)
    call(Common_EventScript_NameReceivedPartyMon)
    setflag(FLAG_GOT_STARTER)
    playfanfare(FANFARE_RG_OBTAIN_KEY_ITEM)
    msgbox("FROAKIE joined your squad.")
    return
}

# =========================================
# 4) POST-STARTER: bursting ranger + storm → deck → beach
# Requires a hidden OBJ_BURST_RANGER in Captain's room (set Hidden in Porymap).
# =========================================

movement BurstRanger_Enter {
    walk_fast_up
    walk_fast_up
    walk_fast_up
    walk_left
    face_right
    }

script PostStarter_StormInterrupt {
    if (flag(FLAG_STORM_STARTED)) { end }
    # Ranger bursts in
    msgbox(format("Mara: Now, listen up, I -\p"))
    addobject(OBJ_BURST_RANGER)
    showobjectat(OBJ_BURST_RANGER, MAP_TIDESMERCYINTERIOR)
    playse(SE_EXIT)
    waitse
    setvar(VAR_UNUSED_0x404E, 1)
    applymovement(OBJ_BURST_RANGER, BurstRanger_Enter)
    waitmovement(0)
    msgbox(format(
        "Ranger: Captain! Massive storm just appeared out of nowhere!"
        "The barometer is going haywire, it's right on top of us!"
    ))
    waitmessage
    applymovement(OBJ_CAPT_MARA, moves(emote_exclamation_mark))
    playse(SE_PIN)
    waitmovement(0)
    applymovement(OBJ_CAPT_MARA, moves(walk_in_place_fast_left walk_fast_down walk_fast_down walk_fast_down))
    waitmovement(0)
    playse(SE_EXIT)
    waitse
    removeobject(OBJ_CAPT_MARA)
    msgbox(format
        ("Captain Mara: Everybody move! All hands topside!"))
    setflag(FLAG_STORM_STARTED)
    fadescreen(FADE_TO_BLACK)
    warp(MAP_SS_TIDES_MERCY, 19, 8)
    fadescreen(FADE_FROM_BLACK)
    end
}